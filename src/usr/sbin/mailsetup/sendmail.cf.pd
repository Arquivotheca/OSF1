#
# *****************************************************************
# *                                                               *
# *    Copyright (c) Digital Equipment Corporation, 1991, 1994    *
# *                                                               *
# *   All Rights Reserved.  Unpublished rights  reserved  under   *
# *   the copyright laws of the United States.                    *
# *                                                               *
# *   The software contained on this media  is  proprietary  to   *
# *   and  embodies  the  confidential  technology  of  Digital   *
# *   Equipment Corporation.  Possession, use,  duplication  or   *
# *   dissemination of the software and media is authorized only  *
# *   pursuant to a valid written license from Digital Equipment  *
# *   Corporation.                                                *
# *                                                               *
# *   RESTRICTED RIGHTS LEGEND   Use, duplication, or disclosure  *
# *   by the U.S. Government is subject to restrictions  as  set  *
# *   forth in Subparagraph (c)(1)(ii)  of  DFARS  252.227-7013,  *
# *   or  in  FAR 52.227-19, as applicable.                       *
# *                                                               *
# *****************************************************************
#
# HISTORY
# 
# @(#)$RCSfile: sendmail.cf.pd,v $ $Revision: 1.1.8.2 $ (DEC) $Date: 1993/09/07 21:42:57 $ 
changequote([,])dnl
define(_TMP, maketemp(m4XXXXX))dnl
syscmd([echo '$Revision: 1.1.8.2 $' | awk '{printf("%s",$2)}' >]_TMP)dnl
define(_Rev, include(_TMP))dnl
syscmd([hostname | awk '{printf("%s",$0)}' >]_TMP)dnl
define(_MyFullName, include(_TMP))dnl
syscmd([hostname | awk -F. '{printf("%s",$1)}' >]_TMP)dnl
define(_MyUnqualName, include(_TMP))dnl
syscmd([uname | awk '{printf("%s",$0)}' >]_TMP)dnl
define(_Uname, include(_TMP))dnl
syscmd([date +%d%b%y-%I%M%p | awk '{printf("%s",$0)}' >]_TMP)dnl
define(_ConfigDate, include(_TMP))dnl
syscmd([rm ]_TMP)dnl
changecom()dnl
changequote({,})dnl
include(_ConfigFile)dnl
undefine({include})dnl
undefine({undefine})dnl
dnl--------
define(_Die,{errprint({$1} must be set in the driver M4 file
){}m4exit(1)})dnl
define(_Chk,{ifdef({$1},{},{_Die({$1})})})dnl
dnl--------
#(begin m4 junk)
# _Chk({_MyDomain}) _Chk({_MyDomains}) _Chk({_ParentDomain}) dnl
# _Chk({_MyNicknames}) _Chk({_ExportedName}) _Chk({_MailHub}) dnl
# _Chk({_MailCluster}) _Chk({_NonHiddenUsers}) _Chk({_TrustedUsers}) dnl
# _Chk({_QueueDir}) _Chk({_RFC976}) _Chk({_UnqualifiedW}) dnl
# _Chk({_GateUsenet}) dnl
# dnl
# _Chk({_GateDomain}) _Chk({_TransDomain}) dnl
# _Chk({_GateParent}) _Chk({_TransParent}) dnl
# dnl
# _Chk({_POP}) _Chk({_HostPOP}) _Chk({_TagPOP}) dnl
# dnl
# _Chk({_GateINET}) _Chk({_TransINET}) dnl
# dnl
# _Chk({_UUCP}) dnl
# _Chk({_MyUUCPname}) _Chk({_GateUUCP}) _Chk({_TransUUCP}) _Chk({_UuxArgs}) dnl
# dnl
# _Chk({_DECNet}) _Chk({_DollarY}) _Chk({_AddMail11Cl}) _Chk({_Mail11path}) dnl
# _Chk({_UTK_Mail11}) _Chk({_UMC}) _Chk({_GateMsgRoutCl}) dnl
# _Chk({_MsgRoutDomain}) _Chk({_GateMR}) _Chk({_TransMR}) dnl
# _Chk({_PhaseVdomain}) _Chk({_GateV}) _Chk({_TransV}) _Chk({_PhaseVns}) dnl
# _Chk({_PhaseIVdomain}) _Chk({_GateIV}) _Chk({_TransIV}) dnl
# _Chk({_ReversePhaseIV}) dnl
# _Chk({_IDA}) _Chk({_AliasesLocal}) _Chk({_RelayAll}) _Chk({_MyVname}) dnl
#
#(end m4 junk)
dnl--------
dnl
dnl SharedAliases is to be set if either MailHub or MailCluster is set
dnl
define({_SharedAliases}, ifelse(_MailHub//_MailCluster,{//},{},{T}))dnl
dnl
dnl OSF/1 moved some things from where the rest of the world keeps them
dnl
ifelse(_Uname,{OSF1},dnl {then Uname==OSF1}
{define(_AliasFile,	/var/adm/sendmail/aliases)}dnl
{define(_StatusFile,	/var/adm/sendmail/sendmail.st)}dnl
{define(_HelpFile,	/usr/share/lib/sendmail.hf)}dnl
,dnl {else Uname!=OSF1}
{define(_AliasFile,	/etc/aliases)}dnl
{define(_StatusFile,	/etc/sendmail.st)}dnl
{define(_HelpFile,	/usr/lib/sendmail.hf)}dnl
)dnl {endif Uname}
#
# cruft to let old bindsetup work.  these macros are not used.
#
DDLOCAL
DA{}_MyUnqualName
############################################################ @Ltag(Intro)
############################################################
#####
#####		SENDMAIL CONFIGURATION FILE
#####
#####	Paul Vixie, forked off on 9-December-90
#####	Paul Vixie, major MAIL11/MR work on 17-July-1991
#####	Paul Vixie, Todd Kaehler and Fred Avolio: parameterized, August 1992
#####
#####
############################################################
############################################################

## predefined
#	$y decnet node name (if known) (if running a recent binary)
#	$w fully-qualified hostname
#	$j same as $w (except IDA, which is all-lower-case, strip-at-first-dot)
##

############################################################ @Ltag(LocalInfo)
###	local info
############################################################

# parent domain
DP{}_ParentDomain

# my domain
DN{}_MyDomain

# name exported on external internet mail
DW{}_ExportedName

# my official hostname
ifelse(_MyDomain,{LOCAL},dnl {then MyDomain==LOCAL}
Dj$w
,dnl {else MyDomain!=LOCAL}
ifelse(_UnqualifiedW,{},dnl {then $w is qualified}
Dj$w
,dnl {else $w is unqualified}
Dj$w.$N
)dnl {endif UnqualifiedW}
)dnl {endif MyDomain}

ifelse(_DECNet,{},{},dnl {else}
ifelse(_DollarY,{},{},dnl
Dy{}_DollarY
)dnl {endif DollarY}
# $=J[.$P] is gatewayed through $J or sent via DECNET/MAIL11
# $=K[.$P] is gatewayed through $K or sent via DECNET/UMC
#	if $J ($K) is undefined; will just use MX records (i.e. $#smtp)
#	if set to $j will cause mail to be sent via mail11 from this host
# 
# Easynet/Mail11
#
# (DNET is hardcoded here because DEC's mail11d generates it; $=J should not
#  include $E or $F since these are likely to be multi-token which only IDA
#  can handle in class matches. UTK Mail11D generates .DNET or .ENET (depending
#  on your compile-time configuration).  $=J must include whatever your Mail11D
#  is generating which in the case of UTK with .ENET would make you want to
#  set your _AddMail11Cl to ENET.)
#
CJDNET _AddMail11Cl
ifelse(_GateIV,{},{dnl},DJ{}_GateIV)
DE{}_PhaseIVdomain
ifelse(_GateV,{},{dnl},DI{}_GateV)
DF{}_PhaseVdomain
)dnl {endif _DECNet}
ifelse(_UMC,{},{},dnl
{#
# MessageRouter/UMC
#
CKUMC _GateMsgRoutCl
ifelse(_GateMR,{},{dnl},DK{}_GateMR)
DM{}_MsgRoutDomain
})dnl {endif UMC}
ifelse(_UUCP,{},{},dnl
ifelse(_GateUUCP,{},dnl
#
# UUCP
#
DU{}_MyUUCPname
)dnl {endif _GateUUCP}
)dnl {endif _UUCP}
ifelse(_MyNicknames,{},{},dnl
{#
# aliases for my machine
#
Cw{}_MyNicknames
})dnl {endif MyNicknames}

#
# aliases at the $P level -- we think we are authoritative for these
#
Cd{}_MyDomains

#
# usernames which will be @host qualified on outbound localdom mail
#
CN{}_NonHiddenUsers

#
# trash top-level domains -- don't use $[...$] on names ending with these
#
ifelse(_X25Mail,{},
CT{}UUCP USENET UMC DNET _AddMail11Cl
,dnl
CT{}UUCP USENET UMC DNET X25 _AddMail11Cl

#
# My X.25 node name
#
DS{}_MyUnqualName
)dnl	{endif X25Mail}

############################################################
#
#	General configuration information
#
############################################################

# Version Number
DV{}_Rev{/}_ConfigDate

##########################
###   Special macros   ###
##########################

# my name
DnMAILER-DAEMON
# UNIX header format
DlFrom $g  $d
# delimiter (operator) characters
Do.:%@!^=/[]
# format of a total name
ifelse(_RFC976,{},dnl {non-rfc976; use "comment <addr>"}
Dq$?x$x $.<$g>
,dnl {rfc976 calls for "addr (comment)"}
Dq$g$?x ($x)$.
)dnl {endif RFC976}
# SMTP login message
De$j Sendmail $v ($V) $b

###################
###   Options   ###
###################

# wait 5 minutes for newaliases to complete
Oa
# location of alias file
OA{}_AliasFile
# default delivery mode (deliver in background)
Odbackground
# temporary file mode
OF0600
# default UID
Ou1
# default GID
Og1
# location of help file
OH{}_HelpFile
# log level
OL9
# include sender if she's on an alias to which she's sending
Om
# queue directory
OQ{}_QueueDir
# read timeout -- violates protocols
Or1h
# status file
OS{}_StatusFile
# queue up everything before starting transmission
Os
ifelse(_IDA,{},{},dnl
# use separate envelope/header rewriting rulesets (IDA)
O/
)dnl
# default timeout interval
OT3d
# load average for forcing "Odq" behaviour
Ox20
# load average for refusing connections
OX12

###############################
###   Message precedences   ###
###############################

Pfirst-class=0
Pspecial-delivery=100
Pjunk=-100

#########################
###   Trusted users   ###
#########################

T{}_TrustedUsers

#############################
###   Format of headers   ###
#############################

H?P?Return-Path: <$g>
#HReceived: by $j; id $i; $b
HReceived: $?sfrom $s $.by $j; ($v/$V)
	id $i; $b
H?D?Resent-Date: $a
H?D?Date: $a
H?F?Resent-From: $q
H?F?From: $q
H?x?Full-Name: $x
HSubject:
H?M?Resent-Message-Id: <$t.$i@$j>
H?M?Message-Id: <$t.$i@$j>

###########################
###   Rewriting rules   ###
###########################

##
## S1/S2 (and S5/S6 in IDA) are called before the per-mailer S= and R= rwsets.
## for any address types which we export in native format (e.g., uucp !-form)
## rather than pseudodomain format (e.g., mail11 @node.decnet form), we need
## to undo the effects of S3/S7 here, so that the per-mailer rwsets will see
## them as "unqualified" and add the appropriate qualification to them.  note
## that this is a deeply divisive issue; these so-called "mixed-mode" addresses
## (e.g., vixie!paul@decwrl.dec.com) have different interpretations depending
## on who you ask.  since you're running our config file, you're getting our
## particular leanings.  if you want it done some other way, do it yourself.
## (vix)

################################
#  Sender Field Pre-rewriting  #
################################
S1
R$+<@$-.UUCP>		$2!$1				decapsulate UUCP

###################################
#  Recipient Field Pre-rewriting  #
###################################
S2
R$+<@$-.UUCP>		$2!$1				decapsulate UUCP

###########################
#  Name Canonicalization  #
###########################
S3

# handle "from:<>" special case
R<>			$@@				turn into magic token

# route-addr's look canonical but aren't
R<@$+:$+>$*		@$1:$2				unfocus <route-addr>

# already canonical?
R$*<@$+>$*		$@$>7$1<@$2>$3			idempotency is good

# basic textual canonicalization -- note lack of RFC733 heuristic here
R$*<$+>$*		$2				basic RFC822 parsing

# make sure <@a,@b,@c:user@d> syntax is easy to parse -- undone later
R@$+,$+			@$1:$2				change all "," to ":"
R@$+:$+			$@$>7<@$1>:$2			handle <route-addr>

# more miscellaneous cleanup
R$+:$*;$*		$@$1:$2;$3			list syntax
R$+@$+			$:$1<@$2>			focus on domain
R$+<$+@$+>		$1$2<@$3>			move gaze right
R$+@$+<@$+>		$1%$2<@$3>			a@b@c@d ->a%b%c@d

ifelse(_DECNet,{},{},dnl {DECNet}
#
# DECNet.  First turn all these addresses into :: form then back to @-form
# with a .pseudodomain approporiate to the protocol (phaseIV/phaseV/MR)
#

# (phase IV)
R$+<@$+.$E>		$@$>7$1<@$2.$E>			already encapsulated IV
ifelse(_ReversePhaseIV,{},dnl {then}
R$+<@$-.$-.$=J>		$:$2.$3::$1			u@aa.nnn.DNET
,dnl {else}
R$+<@$-.$-.$=J>		$:$3.$2::$1			u@aa.nnn.DNET
)dnl {endif ReversePhaseIV}
R$+<@$-.$=J>		$:$2::$1			u@node.DNET

R$+<@$+.$=J>		$1<@$2>				not IV; try as a V

# (phase V)
R$+<@.$-.$+>		$:.$2.$3::$1			u@.root.path
R$+<@$-:.$-.$+>		$:$2:.$3.$4::$1			u@ns:.root.path
R$+<@%$->		$:%$2::$1			u@%nsap
# (all decnet addresses
#  are now in ::-format,
#  including the ones
#  that came in that way;
#  and the .DNET is gone.)
)dnl {DECNet}

# things that are still in @-form are ready to eat
R$+<@$+>		$@$>7$1<@$2>			now canonical

ifelse(_DECNet,{},{},dnl {DECNet}
# (phase IV conversions)
ifelse(_ReversePhaseIV,{},dnl {then}
R$-.$-::$+		$@$>7$3<@$1.$2.$E>		numeric decnet addr
,dnl {else !ReversePhaseIV}
R$-.$-::$+		$@$>7$3<@$2.$1.$E>		numeric decnet addr
)dnl {endif ReversePhaseIV}
R$-::$+			$@$>7$2<@$1.$E>			h::u -> u@h.pseudodom

# (phase V conversions)
R%$-::$+		$@$>7$2<@$1.$F>			%nsap::user
R.$-.$+::$+		$:_PhaseVns:.$1.$2::$3		add default namespace
# (anything with a :: at
#  this point is a NS-
#  qualified phase V addr;
#  there are NO @-signs)
R$-:.$-.$+::$+		$:$4@$3<@$2.$1>			ns:.root.path
R$+@$-.$+<@$*>		$1@$3<@$2.$4>			shuffle tokens to end
R$+@$-<@$+>		$@$>7$1<@$2.$3.$F>		last token; remove dot
)dnl {DECNet}

ifelse(_UUCP,{},{},dnl
# UUCP conversions
R$-.$+!$+		$@$>7$3<@$1.$2>			host.domain!user
R$-!$+			$@$>7$2<@$1.UUCP>		resolve uucp names
)dnl {endif UUCP}

# convert rightmost % to @ (ruleset 7 has the rest of the magic for this)
R$+%$+			$@$>7$1<@$2>			user%host

# (ruleset 3 ends here.  we don't exit through
#  $>7 here as with the other exits from ruleset 3
#  since we only exit through S7 when we make a
#  change.)

##### special local conversions (exit path from S3)
S7
R$*<@$+%$+>$*		$1%$2<@$3>$4			move @ right after %
ifelse(_DECNet,{},{},dnl
{R$*<@$+.$E>$*		$@$1<@$2.$E.$P>$3		hide .$E under $P
R$*<@$+.$F>$*		$@$1<@$2.$F.$P>$3		hide .$F under $P
})dnl {DECNet}
ifelse(_UMC,{},{},dnl
R$*<@$+.$=K>$*		$@$1<@$2.$M.$P>$4		.$=K -> $M.$P
)dnl {UMC}
ifelse(_HostPOP,{},{},dnl
R$+<@POP>$*		$@$1<@POP>$2			POP already OK
)dnl {endif HostPOP}
ifelse(_MyDomain,{LOCAL},{},dnl
R$*<@$->$*		$@$1<@$2.$N>$3			qualify hostnames
)dnl {endif MyDomain}

#################################
#  Final Output Post-rewriting  #
#################################
S4

R@			$@				handle <> error addr
R$*<$+>$*		$1$2$3				defocus
R@$+:$+:$+		@$1,$2:$3			<route-addr> canonical
R@$+:$+			$@<@$1:$2>			route-addr needs <>

#################################
#  (IDA) header senders         #
#################################
S5
R$+			$@$>1$1				same as envelope

#################################
#  (IDA) header recipients      #
#################################
S6
R$+			$@$>2$1				same as envelope

#################################
#  Utility: strip local domain  #
#################################

# this is neccessarily tricky.  S0 needs to strip off the local host(s) and
# it may take several passes to do that (consider $-%$j%$j%$j or $U!$-@$j).
# this requires iterate-until-no-change, which is a semantic not directly
# provided by sendmail.  sendmail does permit recursion, though, which we
# use.  S0 calls S8, which calls itself whenever it makes a change, else falls
# out the bottom when it has nothing left to strip.
#
# S8 starts by calling S3 since on the subsequent recursive calls, a change
# will have been made and the canonicalizion will have been lost.  we would
# like to use $>8$>3 but that doesn't work the way you'd expect.  the cost
# here is that S3 gets called once at the top even though S3 was called before
# S0 so there's nothing for it to do.  this is why S3 must be idempotent and
# also why S3's "no change needed" condition is so close to its top.

S8

R$+			$:$>3$1				needed for recursions

R$*<@$j>$*		$:$1<@>$2			@myfqdm
R$*<@$=w>$*		$:$1<@>$3			@my alias
R$*<@$=w.$N>$*		$:$1<@>$3			@my alias

ifelse(_MyDomain,{LOCAL},{},dnl {non-LOCAL}
ifelse(_SharedAliases,{},{},dnl {SharedAliases}
# next two lines only gen'd if have aliases for all and $N isn't LOCAL
R$*<@$N>$*		$:$1<@>$2			@mydomain
R$*<@$=d.$P>$*		$:$1<@>$3			@codomain
)dnl {endif SharedAliases}
)dnl {endif MyDomain}

ifelse(_DECNet,{},{},dnl
R$*<@$y.$E.$P>$*	$:$1<@>$2			myname:: (IV)
R$*<@$y.$F.$P>$*	$:$1<@>$2			myname:: (V)
)dnl {endif DECNet}

ifelse(_UUCP,{},{},dnl
R$*<@$U.UUCP>$*		$:$1<@>$2			myuucpname!
)dnl {endif UUCP}

ifelse(_X25Mail,{},{},dnl
R$*<@$S.x25>$*		$:$1<@>$2			myname.x25
R$*<@$S.x25.$P>$*	$:$1<@>$2			myname.x25
)dnl {endif X25Mail}

# here's where we recurse if a change was made
R<@>:$*			$@$>8$1				localhost/route-addr
R$*<@>$*		$@$>8$1$2			localhost

########################################
#  Utility: canonicalize local domain  #
########################################

S9

ifelse(_MyDomain,{LOCAL},{},dnl
{# (next four lines are only generated if $N isn't LOCAL)
R$*<@$->$*		$:$1<@$2.$N>$3			qualify
R$*<@$+.$=T>$*		$@$1<@$2.$3>$4			trash pseudodom, lv aln
R$*<@$+.$=T.$P>$*	$@$1<@$2.$3.$P>$4			qualified trash, lv aln
R$*<@$+>$*		$:$1<@$[$2$]>$3			canon'ize complex names
})dnl {endif _MyDomain}

############################################################
############################################################
#####
#####			RULESET 0
#####
############################################################
############################################################

S0

# special cases
#
R@			$#local $:$n			handle <> form
R$*<@[$+]>$*		$#smtp $@[$2] $:$1<@[$2]>$3	numeric internet spec

# localize if possible (rip off @DOMAINs for which we are authoritative)
#
R$*<@$+>$*		$:$>8$1<@$2>$3
ifelse(_DECNet,{},{},dnl

# DECNet/Mail11
#
# (phase IV)
R$+<@$+.$E.$P>		$:$2@@$1<@$2.$E.$P>		dup nodename; add @@
ifelse(_ReversePhaseIV,{T},dnl {then}
R$-.$-@@$+		$:$2.$1@@$3			unreverse
,)dnl {endif}
R$+@@$+<@$+>	$:$?J$1@@$2??$3<@$J>$|$1@@$2<@$3>$.	mk @relay or lv alone
R$+@@$+??$+<@$j>		$#mail11 $@$1 $:$2	them is us; use mail11
R$+@@$+??$+<@$J>		$#_TransIV $@$J $:$2<@$3>	undo damage; send now
R$+@@$+??$+<@$+>		$:$2<@$3>			undo $?J damage
R$+@@$+<@$+>		$:$2<@$3>			undo !$?J damage
# (phase V)
R$+<@$+.$F.$P>		$:@@$2@@$1<@$2.$F.$P>		dup nodename; add @@ @@
R$*@@$-.$+@@$+		$2.$1@@$3@@$4			slide tokens to left@@
R$+.@@$-@@$+		$2:.$1@@$3			rm 1@@; final reformat
R@@$-@@$+		%$1@@$2			rm 1@@; final reformat
R$+@@$+<@$+>	$:$?I$1@@$2??$3<@$I>$|$1@@$2<@$3>$.	mk @relay or lv alone
R$+@@$+??$+<@$j>		$#mail11 $@$1 $:$2		them is us; use mail11
R$+@@$+??$+<@$I>		$#_TransV $@$I $:$2<@$3>	undo damage; send now
R$+@@$+??$+<@$+>		$:$2<@$3>			undo $?I damage
R$+@@$+<@$+>		$:$2<@$3>			undo !$?I damage
)dnl {_DECNet}
ifelse(_UMC,{},{},dnl
# (MessageRouter)
R$+<@$+.$=K.$P>		$:$?K$1??$2.$M.$P<@$K>$|$1<@$2.$M.$P>$.
R$+??$+.$M.$P<@$j>	$#umc $@$2 $:$1<@$2>		them is us; use mail11
R$+??$+.$M.$P<@$K>	$#_TransMR $@$K $:$1<@$2.$M.$P>	undo damage; send now
)dnl {endif UMC}
ifelse(_GateUsenet,{},{},dnl

# .USENET
#
R$+.USENET		$:$1.USENET<@_GateUsenet>	groupname.USENET
)dnl {endif GateUsenet}
ifelse(_UUCP,{},{},dnl

# UUCP
#
ifelse(_GateUUCP,{},dnl {then}
R$*<@$-.UUCP>		$#uucp $@$2 $:$2!$1		use our own uucp
,dnl {else GateUUCP}
R$*<@$-.UUCP>		$#_TransUUCP $@_GateUUCP $:$2!$1<@_GateUUCP>	use GW
)dnl {endif GateUUCP}
)dnl {endif UUCP}
ifelse(_X25Mail,{},{},dnl

# X25 (PSI) Mail
#
R$+<@$+.x25>		$#Xmail $@$2.x25 $:$1
)dnl {endif X25Mail}
ifelse(_POP,{},{},dnl
ifelse(_HostPOP,{},{},dnl

# Fake hosts in my domain
#
R$+<@POP>		$#pop $@$j $:$1			username@POP
)dnl {endif HostPOP}
)dnl {endif POP}
ifelse(_RelayAll,{},dnl {relay all nonlocal mail}

# Real hosts in my domain
#
ifelse(_GateDomain,{},dnl {GateDomain empty, go direct}
ifelse(_MyDomain,{LOCAL},dnl {then MyDomain==LOCAL}
R$*<@$->$*	$#_TransDomain $@$2 $:$1<@$2>$3		LOCAL; no gw
,dnl {else MyDomain!=LOCAL}
R$*<@$*$N>$*	$#_TransDomain $@$2$N $:$1<@$2$N>$3	our domain; no gw
)dnl {endif MyDomain}
,dnl {else GateDomain nonempty, use it}
ifelse(_MyDomain,{LOCAL},dnl {then MyDomain==LOCAL}
R$*<@$->$*	$#_TransDomain $@_GateDomain $:$1<@$2>$3	LOCAL; gw
,dnl {else MyDomain!=LOCAL}
R$*<@$*$N>$*	$#_TransDomain $@_GateDomain $:$1<@$2$N>$3	our domain; gw
)dnl {endif MyDomain}
)dnl {endif GateDomain}
ifelse(_ParentDomain,{LOCAL},{},dnl {if ParentDomain is local, so is MyDomain}

# Parent domain
#
ifelse(_GateParent,{},dnl {GateParent empty, go direct}
R$*<@$*$P>$*	$#_TransParent $@$2$P $:$1<@$2$P>$3	our domain; no gw
,dnl {else GateParent nonempty, use it}
R$*<@$*$P>$*	$#_TransParent $@_GateParent $:$1<@$2$P>$3	our domain; gw
)dnl {endif GateDomain}
)dnl {endif ParentDomain}

# Nonlocal mail
#
ifelse(_GateINET,{},dnl {then}
R$*<@$+>$*		$#smtp $@$2 $:$1<@$2>$3			go direct
,dnl {else GateINET}
R$*<@$+>$*		$#_TransINET $@_GateINET $:$1<@$2>$3	use gateway
)dnl {endif GateINET}

# Local mail
#
ifelse(_TagPOP,{},{},dnl

R$+.POP			$#pop $@$j $:$1			username.POP
)dnl {endif TagPOP}

R$+			$#local $:$1			must be local
,dnl {else RelayAll}
R$=N		$#local $:$1			this is local
ifelse(_IDA,{},{},dnl
{R$-		$:$1??$(@ $1 $: $)		Look up local aliases
R$-??$+		$#local $:$1			Found it
R$-??		$:$1				undo dammage
})dnl
R$+		$#_TransINET $@_GateINET $:$1	all other mail; gw
)dnl {endif RelayAll}

############################################################
############################################################
#####
#####		Local, POP and Program mailers
#####
############################################################
############################################################

Mlocal,	P=/bin/mail, F=lsDFMmn, S=10, R=20, A=mail -r $f -d $u
Mprog,	P=/bin/sh,   F=lsDFM,   S=10, R=20, A=sh -c $u
Mpop,	P=/usr/lib/mh/spop, F=nsmFDM, S=10, R=20, A=pop $u

S10
R@			$n				errors to mailer-daemon

S20

############################################################
############################################################
#####
#####		SMTP to hosts inside the local domain
#####
############################################################
############################################################

Msmtpl,	P=[IPC], F=mDFMuX, S=11, R=21, A=IPC $h, E=\r\n

# (we pull off the local hostname in all cases, and then selectively add it
#  back on.  we would add it back if the aliases aren't shared across all
#  hosts in this domain, or if the user is root or one of the others that
#  we want to know the hostname for.)
#
S11
R$*<@$j>$*		$>3$1$2				strip local host name
R$*<@$+>$*		$:$>9$1<@$2>$3			canonicalize domain
ifelse(_MailCluster,{},dnl
# (next two lines are only present if aliases aren't shared across localdom)
R$*<@$+>$*		$@$1<@$2>$3			already has an @domain
R$+			$@$1<@$j>			add our hostname
,dnl {else}
# (next line is only present if aliases are shared across localdom)
R$=N			$@$1<@$j>			qualify nonhidden users
ifelse(_IDA,{},{},dnl
ifelse(_AliasesLocal,{},{},dnl
{R$-			$:$1??$(@ $1 $: $)		Look up local aliases
R$-??$+			$@$1<@$j>			Found it - reformat
R$-??			$:$1				undo dammage
})dnl
)dnl
{})dnl {endif MailCluster}

# (by running S11 on recipients, we get the above behaviour on To: and Cc:
#  headers, as well as for the envelope recipient.  this is good since we
#  always call $#smtpl with hostname attached, which means that we won't be
#  down here at all if $j is in the address (that would have been stripped
#  in S0), and we will therefore qualify any domain-less addresses with our
#  own hostname before sending them to other hosts in our domain.  this is
#  all ugly but it's what you have to do if your aliases aren't shared. --vix)
#
S21
R$+			$@$>11$1			nothing special here

############################################################
############################################################
#####
#####		SMTP to hosts outside the local domain
#####
############################################################
############################################################

Msmtp,	P=[IPC], F=mDFMuXL, S=12, R=22, A=IPC $h, E=\r\n

S12
R<@$+>$*		$@<@$1>$2			<route-addr> syntax
R$*<@[$+]>$*		$@$1<@[$2]>$3			numeric, lv alone
R$*<@$+>$*		$@$>9$1<@$2>$3			canonicalize domain
R$=N			$@$1<@$j>			Add $j to local
ifelse(_IDA,{},{},dnl
ifelse(_AliasesLocal,{},{},dnl
{R$-			$:$1??$(@ $1 $: $)		Look up aliases
R$-??$+			$@$1<@$j>			Found it - reformat
R$-??			$:$1				undo dammage
})dnl
)dnl
R$+			$@$1<@$W>			fix up return addr

S22
R$+			$@$>12$1			nothing special here

############################################################
############################################################
#####
#####		SMTP to relay host
#####
############################################################
############################################################

Msmtpr,	P=[IPC], F=mDFMuX, S=13, R=23, A=IPC $h, E=\r\n

# (we pull off the local hostname in all cases, and then selectively add it
#  back on.  we would add it back if the user is root or one of the others
#  for which we want to know the hostname.  Otherwise we want to look like we
#  are a local user on the relay machine.  This is to allow for "cleaner"
#  addresses when crossing mail domains. --FMA)
#
S13
R$*<@$j>$*		$>3$1$2				strip local host name
R$*<@$+>$*		$:$>9$1<@$2>$3			canonicalize domain
R$=N			$@$1<@$j>			qualify nonhidden users
ifelse(_IDA,{},{},dnl
ifelse(_AliasesLocal,{},{},dnl
{R$-			$:$1??$(@ $1 $: $)		Look up aliases
R$-??$+			$@$1<@$j>			Found it - reformat
R$-??			$:$1				undo dammage
})dnl
)dnl

S23
R$+			$@$>13$1			nothing special here

ifelse(_DECNet,{},{},dnl
{############################################################
############################################################
#####
#####           DECNET/MAIL11
#####
############################################################
############################################################

ifelse(_UTK_Mail11,{},dnl {!UTK}
{# (note that you will need an ultrix or osf1 sendmail binary to use this)
Mmail11, P=_Mail11path, F=mnSXxH, S=14, R=24/29,
	A=mail11 $f $x $h
},dnl {UTK}
{# (this is unsupported software on gatekeeper.dec.com:~ftp/pub/DEC/gwtools/)
Mmail11, P=_Mail11path, F=mSnsF, S=14, R=24/29,
	A=mail11 -alwaystemp $g $x $h $u
})dnl {endif UTK}

# (mail11 senders)
S14
R$+			$:$>3$1			Make canonical to convert last % to @
# (phase IV)
ifelse(_ReversePhaseIV,{T},dnl {then}
R$+<@$-.$-.$E.$P>	$@$3.$2::$1			unreverse
,)dnl {endif}
R$+<@$+.$E.$P>		$@$2::$1			phaseIV back to :: form
# (phase V)
R$+<@$-.$F.$P>		$@$1@%$2			% form
R$+<@$+.$F.$P>		$:@@$2@@$1			dup nodename; add @@ @@
R$*@@$-.$+@@$+		$2.$1@@$3@@$4			slide tokens to left@@
R$+.@@$-@@$+		$@$3@$2:.$1			rm 1@@; final reformat

# (mail11 recip env)
S24
R$+			$@$>14$1			nothing special here

# (mail11 recip hdr)
S29
# next line is for CC: headers which most mail11 receivers won't qualify
ifelse(_MyVname,{},dnl
R$-			$@$y::$1			tack on our nodename
,dnl
R$-			$@$1@$y				tack on our nodename
)dnl
R$+			$@$>14$1			nothing special here

ifelse(_UMC,{},{},dnl
{############################################################
############################################################
#####
#####           UMC mailer
#####
############################################################
############################################################

Mumc,	P=/usr/lib/umc/mr_mailer, F=mnDFMS, R=0, S=0,
	A=mr_mailer -send -hop $c -from $f -to $u

})dnl {endif UMC}
})dnl {endif DECNet}

ifelse(_UUCP,{},{},dnl
ifelse(_GateUUCP,{},dnl
{############################################################
############################################################
#####
#####           UUCP mailers
#####
############################################################
############################################################
#
{Muucp,	P=/usr/bin/uux, F=sDFhuU, S=15, R=25, M=100000,}
	A=uux - _UuxArgs $h!rmail ($u)

##
## a more robust implementation of uucp would provide for route-addrs and
## MX neighbors; as well as pathalias; pulling $P off of pseudodomains;
## and RFC822-vs-not neighbors.
##
## UUCP is an odd case since the overwhelming majority of UUCP nodes can
## handle u@dom form addresses now.  We want to pass those through without
## change.  If something comes here in !-form; we likewise leave it alone
## if the first uucpnode in the path is a single token; if it contains
## multiple dot-separated tokens we assume that it's a domain and we change
## it into @-notation.
##
## note that we do not remove $U from the recipients.  the envelope recipient
## has this done to it at S0 time; and the header recipients should be left
## alone.
##

S15
R$*<@$+>$*		$@$1<@$2>$3		domain form is ok
R$+			$:$>25$1		do unto sender as to recipient
R$U!$+			$@$U!$1			already has our node name
R$+			$@$U!$1			tack on our hostname.

S25
R$*<@$+>$*		$@$1<@$2>$3		domain form is ok
R$-!$+			$@$1!$2			!-form is ok; done
R$+!$+			$@$>3$2<@$1>		looks like domain; chg to u@dom
})dnl {endif GateUUCP}
)dnl {endif UUCP}

ifelse(_X25Mail,{},{},dnl
{###########################################################
############################################################
#####
#####           x25mail Mailer
#####
############################################################
############################################################
#
MXmail, P=/usr/sbin/x25mail, F=mnSXxh, S=16, R=26/28, A=x25mail $f $x $h

# x25mail senders
S16
R$+			$:$>3$1		Make canonical to convert last % to @
R$*<@$S.x25>		$@$1<@$S.x25>	already has our node name
R$*<@$+>		$@$1%$2<@$S.x25>	tack on our name
R$+			$@$1<@$S.x25>	tack on our name

# x25mail Recipient env
S26
R$+			$@$>3$1		Make canonical to convert last % to @

# x25mail Recipient hdr
# Qualify our CC's w. our nodename
S28
R$+			$@$>14$1
})dnl {_X25Mail}
