/*
 * *****************************************************************
 * *                                                               *
 * *    Copyright (c) Digital Equipment Corporation, 1991, 1994    *
 * *                                                               *
 * *   All Rights Reserved.  Unpublished rights  reserved  under   *
 * *   the copyright laws of the United States.                    *
 * *                                                               *
 * *   The software contained on this media  is  proprietary  to   *
 * *   and  embodies  the  confidential  technology  of  Digital   *
 * *   Equipment Corporation.  Possession, use,  duplication  or   *
 * *   dissemination of the software and media is authorized only  *
 * *   pursuant to a valid written license from Digital Equipment  *
 * *   Corporation.                                                *
 * *                                                               *
 * *   RESTRICTED RIGHTS LEGEND   Use, duplication, or disclosure  *
 * *   by the U.S. Government is subject to restrictions  as  set  *
 * *   forth in Subparagraph (c)(1)(ii)  of  DFARS  252.227-7013,  *
 * *   or  in  FAR 52.227-19, as applicable.                       *
 * *                                                               *
 * *****************************************************************
 */
/*
 * HISTORY
 */
/*	
 *	@(#)$RCSfile: kn230_copy.s,v $ $Revision: 1.2 $ (DEC) $Date: 1992/01/15 01:14:06 $
 */ 
/*
 */
/*
 * (c) Copyright 1990, OPEN SOFTWARE FOUNDATION, INC.
 * ALL RIGHTS RESERVED
 */
/*
 * OSF/1 Release 1.0
 */
/* 
 * derived from kn230_copy.s	4.2      (ULTRIX)        1/3/91"; 
 */

/* ------------------------------------------------------------------ */
/* | Copyright Unpublished, MIPS Computer Systems, Inc.  All Rights | */
/* | Reserved.  This software contains proprietary and confidential | */
/* | information of MIPS and its suppliers.  Use, disclosure or     | */
/* | reproduction is prohibited without the prior express written   | */
/* | consent of MIPS.                                               | */
/* ------------------------------------------------------------------ */

/************************************************************************
 *
 *	Modification History: kn230_copy.s
 *
 * 15-Feb-1991  Don Dutile
 *	Merge into osc.25 pool.
 *
 * May-23-1990	Paul Grist 
 *	Created this file which contains specific copy routines for
 *   	use by the mipsmate scsi driver (sii). Use of these modified
 *	copy routine provides a work-around for a mipsmate hardware
 *	problem which is considered to be a high risk h/w fix. These
 *	routines were written by Eric Noya (mipsmate firmware), who modified
 *      the original bcopy routines for use by the firmware driver. In
 *	addition to working around the h/w problem, the routines have
 *      been optimized to the mipsmate (and R3000) architecture to take
 *	advantage of paged-mode I/O, this results in higher than expected
 *	scsi performance for mipsmate.
 *	
 ***********************************************************************/

#ifdef PROM
#include "machine/asm.h"
#include "machine/regdef.h"
#else
#include <machine/asm.h>
#include <machine/regdef.h>
#endif

#define	NBPW	4


/*
 * kn230_bzero(dst, bcount)
 * Zero block of memory
 *
 * Calculating MINZERO, assuming 50% cache-miss on non-loop code:
 * Overhead =~ 18 instructions => 63 (81) cycles
 * Byte zero =~ 16 (24) cycles/word for 08M44 (08V11)
 * Word zero =~ 3 (6) cycles/word for 08M44 (08V11)
 * If I-cache-miss nears 0, MINZERO ==> 4 bytes; otherwise, times are:
 * breakeven (MEM) = 63 / (16 - 3) =~ 5 words
 * breakeven (VME) = 81 / (24 - 6)  =~ 4.5 words
 * Since the overhead is pessimistic (worst-case alignment), and many calls
 * will be for well-aligned data, and since Word-zeroing at least leaves
 * the zero in the cache, we shade these values (18-20) down to 12
 */
#define	MINZERO	4

LEAF(kn230_bzero)
	subu	v1,zero,a0		# number of bytes til aligned
	blt	a1,MINZERO,bytezero
	and	v1,NBPW-1
	subu	a1,v1
	beq	v1,zero,blkzero		# already aligned
#ifdef MIPSEB
	swl	zero,0(a0)
#endif
#ifdef	MIPSEL
	swr	zero,0(a0)
#endif
	addu	a0,v1
	addu	a0,4
	and	a0,0xfffffff8		# align on an 8 byte boundary
/*
 * zero 32 byte, aligned block
 */
blkzero:
	and	a3,a1,~31		# 32 byte chunks
	subu	a1,a3
	beq	a3,zero,wordzero
	addu	a3,a3,a3
	addu	a3,a0			# dst endpoint
1:	sw	zero,0x0(a0)
	sw	zero,0x8(a0)
	sw	zero,0x10(a0)
	sw	zero,0x18(a0)
	addu	a0,0x40
	sw	zero,-0x20(a0)
	sw	zero,-0x18(a0)
	sw	zero,-0x10(a0)
	sw	zero,-0x08(a0)
	bne	a0,a3,1b
wordzero:
	and	a3,a1,~(NBPW-1)		# word chunks
	subu	a1,a3
	beq	a3,zero,bytezero	
	addu	a3,a3
	addu	a3,a0			# dst endpoint
1:	addu	a0,NBPW*2		
	sw	zero,-NBPW(a0)
	bne	a0,a3,1b

bytezero:
	ble	a1,zero,zerodone
	addu	a1,a0			# dst endpoint
1:	addu	a0,1
	sb	zero,-1(a0)
	bne	a0,a1,1b
zerodone:
	j	ra
	END(kn230_bzero)

/*
 * kn230_rbcopy(src, dst, bcount)
 *
 * NOTE: the optimal copy here is somewhat different than for the user-level
 * equivalents (kn230_rbcopy in 4.2, memcpy in V), because:
 * 1) it frequently acts on uncached data, especially since copying from
 * (uncached) disk buffers into user pgms is high runner.
 * This means one must be careful with lwl/lwr/lb - don't expect cache help.
 * 2) the distribution of usage is very different: there are a large number
 * of bcopies for small, aligned structures (like for ioctl, for example),
 * a reasonable number of randomly-sized copies for user I/O, and many
 * bcopies of large (page-size) blocks for stdio; the latter must be
 * well-tuned, hence the use of 32-byte loops.
 * 3) this is much more frequently-used code inside the kernel than outside
 *
 * Overall copy-loop speeds, by amount of loop-unrolling: assumptions:
 * a) low icache miss rate (this code gets used a bunch)
 * b) large transfers, especially, will be word-alignable.
 * c) Copying speeds (steady state, 0% I-cache-miss, 100% D-cache Miss):
 * d) 100% D-Cache Miss (but cacheable, so that lwl/lwr/lb work well)
 *	Config	Bytes/	Cycles/	Speed (VAX/780 = 1)
 *		Loop	Word
 *	08V11	1	35	0.71X	(8MHz, VME, 1-Deep WB, 1-way ILV)
 *		4	15	1.67X
 *		8/16	13.5	1.85X
 *		32/up	13.25	1.89X
 *	08MM44	1	26	0.96X	(8MHz, MEM, 4-Deep WB, 4-way ILV)
 *		4	9	2.78X
 *		8	7.5	3.33X
 *		16	6.75	3.70X
 *		32	6.375	3.92X	(diminishing returns thereafter)
 *
 * MINCOPY is minimum number of byte that its worthwhile to try and
 * align copy into word transactions.  Calculations below are for 8 bytes:
 * Estimating MINCOPY (C = Cacheable, NC = Noncacheable):
 * Assumes 100% D-cache miss on first reference, then 0% (100%) for C (NC):
 * (Warning: these are gross numbers, and the code has changed slightly):
 *	Case		08V11			08M44
 *	MINCOPY		C	NC		C	NC
 *	9 (1 byte loop)	75	133		57	93
 *	8 (complex logic)
 *	Aligned		51	51		40	40
 *	Alignable,
 *	worst (1+4+3)	69	96		53	80
 *	Unalignable	66	93		60	72
 * MINCOPY should be lower for lower cache miss rates, lower cache miss
 * penalties, better alignment properties, or if src and dst alias in
 * cache. For this particular case, it seems very important to minimize the
 * number of lb/sb pairs: a) frequent non-cacheable references are used,
 * b) when i-cache miss rate approaches zero, even the 4-deep WB can't
 * put successive sb's together in any useful way, so few references are saved.
 * To summarize, even as low as 8 bytes, avoiding the single-byte loop seems
 * worthwhile; some assumptions are probably optimistic, so there is not quite
 * as much disadvantage.  However, the optimal number is almost certainly in
 * the range 7-12.
 *
 *	a0	src addr
 *	a1	dst addr
 *	a2	length remaining
 */
/*
 * This routine is used to copy from a ram buffer that holds data
 * in alternate 32 bit words. Normally a bcopy, when faced with
 * a mis-alignment would attempt to line up the destination
 * and mis-align the source. This is not good when the source
 * is the ram buffer, due to the 32-bit holes.
 * What is done now is to leave the source aligned and
 * handle the mis-aligned destination, which is at least
 * contiguous. Burns - 12/19/90
 */

#define	MINCOPY	4

LEAF(kn230_rbcopy)
	xor	v0,a0,a1		# bash src & dst for align chk; BDSLOT
	blt	a2,MINCOPY,rbytecopy	# too short, just byte copy
	and	v0,NBPW-1		# low-order bits for align chk
	subu	v1,zero,a0		# -src; BDSLOT
	bne	v0,zero,runaligncopy	# src and dst not alignable

/*
 * src and dst can be simultaneously word aligned
 */
	and	v1,NBPW-1		# number of bytes til aligned
	subu	a2,v1			# bcount -= alignment
	beq	v1,zero,rblkcopy	# already aligned
#ifdef MIPSEB
	lwl	v0,0(a0)		# copy unaligned portion
	swl	v0,0(a1)
#endif
#ifdef MIPSEL
	lwr	v0,0(a0)
	swr	v0,0(a1)
#endif
	addu	a0,v1		# src += alignment
	addu	a0,4
	and	a0,0xfffffff8	# align on an 8 byte boundary
	addu	a1,v1		

/*
 * 32 byte block, aligned copy loop (for big reads/writes)
 */
#ifdef PROM
rblkcopy:
        li      t1,0x9fffffff		# need mask for cashing
        la      v0,1f
        and     v0,v0,t1		# switch to kseg0
        j       v0                      # run cached
#else
rblkcopy:
#endif
1:	and	a3,a2,~31		# total space in 32 byte chunks
	subu	a2,a3			# count after by-32 byte loop done
	beq	a3,zero,rwordcopy	# less than 32 bytes to copy
	addu	a3,a3
	addu	a3,a0			# source endpoint
2:	lw	v0,0x0(a0)
	lw	v1,0x8(a0)
	lw	t0,0x10(a0)
	lw	t1,0x18(a0)
	lw	t2,0x20(a0)
	lw	t3,0x28(a0)
	lw	t4,0x30(a0)
	lw	t5,0x38(a0)
	sw	v0,0(a1)
	sw	v1,4(a1)
	sw	t0,8(a1)
	sw	t1,12(a1)
	sw	t2,16(a1)
	sw	t3,20(a1)
	addu	a0,0x40			
	sw	t4,24(a1)
	sw	t5,28(a1)
	addu	a1,32			# dst+= 32; fills BD slot
	bne	a0,a3,2b

/*
 * word copy loop
 */
rwordcopy:
	and	a3,a2,~(NBPW-1)		# word chunks
	subu	a2,a3			# count after by word loop
	beq	a3,zero,rbytecopy	# less than a word to copy
	addu	a3,a3
	addu	a3,a0			# source endpoint
1:	lw	v0,0(a0)
	addu	a0,NBPW*2
	sw	v0,0(a1)
	addu	a1,NBPW			# dst += 4; BD slot
	bne	a0,a3,1b
	b	rbytecopy

/*
 * deal with simultaneously unalignable copy by aligning source
 * Need to deal with an aligned source since the source is the
 * ram buffer that has data in alternating 32 bit words.
 */
runaligncopy:
	subu	a3,zero,a0		# calc byte cnt to get src aligned
	and	a3,NBPW-1		# alignment = 0..3
	subu	a2,a3			# bcount -= alignment
	beq	a3,zero,rpartaligncopy	# already aligned

1:	subu	a3,1			# decrement byte count
	lb	v0,0(a0)		# get a byte
	addu	a0,1			# bump src pointer
	sb	v0,0(a1)		# store a byte
	addu	a1,1			# bump dest pointer
	bne	a3,zero,1b

	add	a0,4			# push source past 'hole'

/*
 * For the specific ram buffer layout that this code is written for
 * the ram buffer is the source and it is already aligned. Plus
 * it only holds data in every other word (32 bit word, or longword).
 * Thus if we mis-align it, it is real tough to copy. Instead we deal
 * with the mis-aligned destination. Maybe the cache won't like it
 * but it will work. - burns
 */
/*
 * dst unaligned, src aligned loop
 * NOTE: if MINCOPY >= 7, will always do 1 loop iteration or more
 * if we get here at all
 */
rpartaligncopy:
	and	a3,a2,~(NBPW-1)		# space in word chunks
	subu	a2,a3			# count after 'by word' loop
#if MINCOPY < 7
	beq	a3,zero,rbytecopy	# less than a word to copy
#endif
	# due to screwy rambuf source end point is
	# twice as far away as you would expect
	addu	a3,a3			# double the word count to create
	addu	a3,a0			# the source endpoint
1:
	lw	v0,0(a0)		# get next word
#ifdef MIPSEB
	swl	v0,0(a1)		# store left half
	swr	v0,3(a1)		# store right half
#endif
#ifdef MIPSEL
	swr	v0,0(a1)		# store right half
	swl	v0,3(a1)		# store left half
#endif
	addu	a0,NBPW*2		# bump source pointer, skipping 'hole'
	addu	a1,NBPW			# bump dest pointer
	bne	a0,a3,1b		# at end of word copy?


/*
 * brute force byte copy loop, for bcount < MINCOPY + tail of unaligned src
 * note that lwl, lwr, swr CANNOT be used for tail, since the lwr might
 * cross page boundary and give spurious address exception
 */
rbytecopy:
	addu	a3,a2,a0		# source endpoint; BDSLOT
	ble	a2,zero,rcopydone	# nothing left to copy, or bad length
1:	lb	v0,0(a0)
	addu	a0,1
	sb	v0,0(a1)
	addu	a1,1			# BDSLOT: incr dst address
	bne	a0,a3,1b
rcopydone:
	j	ra
	END(kn230_rbcopy)

/*
 * kn230_wbcopy(src, dst, bcount)
 *
 * NOTE: the optimal copy here is somewhat different than for the user-level
 * equivalents (kn230_wbcopy in 4.2, memcpy in V), because:
 * 1) it frequently acts on uncached data, especially since copying from
 * (uncached) disk buffers into user pgms is high runner.
 * This means one must be careful with lwl/lwr/lb - don't expect cache help.
 * 2) the distribution of usage is very different: there are a large number
 * of bcopies for small, aligned structures (like for ioctl, for example),
 * a reasonable number of randomly-sized copies for user I/O, and many
 * bcopies of large (page-size) blocks for stdio; the latter must be
 * well-tuned, hence the use of 32-byte loops.
 * 3) this is much more frequently-used code inside the kernel than outside
 *
 * Overall copy-loop speeds, by amount of loop-unrolling: assumptions:
 * a) low icache miss rate (this code gets used a bunch)
 * b) large transfers, especially, will be word-alignable.
 * c) Copying speeds (steady state, 0% I-cache-miss, 100% D-cache Miss):
 * d) 100% D-Cache Miss (but cacheable, so that lwl/lwr/lb work well)
 *	Config	Bytes/	Cycles/	Speed (VAX/780 = 1)
 *		Loop	Word
 *	08V11	1	35	0.71X	(8MHz, VME, 1-Deep WB, 1-way ILV)
 *		4	15	1.67X
 *		8/16	13.5	1.85X
 *		32/up	13.25	1.89X
 *	08MM44	1	26	0.96X	(8MHz, MEM, 4-Deep WB, 4-way ILV)
 *		4	9	2.78X
 *		8	7.5	3.33X
 *		16	6.75	3.70X
 *		32	6.375	3.92X	(diminishing returns thereafter)
 *
 * MINCOPY is minimum number of byte that its worthwhile to try and
 * align copy into word transactions.  Calculations below are for 8 bytes:
 * Estimating MINCOPY (C = Cacheable, NC = Noncacheable):
 * Assumes 100% D-cache miss on first reference, then 0% (100%) for C (NC):
 * (Warning: these are gross numbers, and the code has changed slightly):
 *	Case		08V11			08M44
 *	MINCOPY		C	NC		C	NC
 *	9 (1 byte loop)	75	133		57	93
 *	8 (complex logic)
 *	Aligned		51	51		40	40
 *	Alignable,
 *	worst (1+4+3)	69	96		53	80
 *	Unalignable	66	93		60	72
 * MINCOPY should be lower for lower cache miss rates, lower cache miss
 * penalties, better alignment properties, or if src and dst alias in
 * cache. For this particular case, it seems very important to minimize the
 * number of lb/sb pairs: a) frequent non-cacheable references are used,
 * b) when i-cache miss rate approaches zero, even the 4-deep WB can't
 * put successive sb's together in any useful way, so few references are saved.
 * To summarize, even as low as 8 bytes, avoiding the single-byte loop seems
 * worthwhile; some assumptions are probably optimistic, so there is not quite
 * as much disadvantage.  However, the optimal number is almost certainly in
 * the range 7-12.
 *
 *	a0	src addr
 *	a1	dst addr
 *	a2	length remaining
 */
#define	MINCOPY	4

LEAF(kn230_wbcopy)
	xor	v0,a0,a1		# bash src & dst for align chk; BDSLOT
	blt	a2,MINCOPY,wbytecopy	# too short, just byte copy
	and	v0,NBPW-1		# low-order bits for align chk
	subu	v1,zero,a0		# -src; BDSLOT
	bne	v0,zero,wunaligncopy	# src and dst not alignable
/*
 * src and dst can be simultaneously word aligned
 */
	and	v1,NBPW-1		# number of bytes til aligned
	subu	a2,v1			# bcount -= alignment
	beq	v1,zero,wblkcopy		# already aligned
#ifdef MIPSEB
	lwl	v0,0(a0)		# copy unaligned portion
	swl	v0,0(a1)
#endif
#ifdef MIPSEL
	lwr	v0,0(a0)
	swr	v0,0(a1)
#endif
	addu	a0,v1			# src += alignment
	addu	a1,v1			# dst += alignment
	add	a1,4
	and	a1,0xfffffff8		# align on an 8 byte boundary

/*
 * 32 byte block, aligned copy loop (for big reads/writes)
 */

#ifdef PROM
wblkcopy:
        li      t1,0x9fffffff		# need mask for cashing
        la      v0,1f
        and     v0,v0,t1		# switch to kseg0
        j       v0                      # run cached
#else
wblkcopy:
#endif
1:	and	a3,a2,~31		# total space in 32 byte chunks
	subu	a2,a3			# count after by-32 byte loop done
	beq	a3,zero,wwordcopy	# less than 32 bytes to copy
	addu	a3,a0			# source endpoint
2:	lw	v0,0(a0)
	lw	v1,4(a0)
	lw	t0,8(a0)
	lw	t1,12(a0)
	sw	v0,0(a1)
	sw	v1,0x8(a1)
	sw	t0,0x10(a1)
	sw	t1,0x18(a1)
	addu	a0,32			# src+= 32; here to ease loop end
	lw	v0,-16(a0)
	lw	v1,-12(a0)
	lw	t0,-8(a0)
	lw	t1,-4(a0)
	sw	v0,0x20(a1)
	sw	v1,0x28(a1)
	sw	t0,0x30(a1)
	sw	t1,0x38(a1)
	addu	a1,0x40			# dst+= 32; fills BD slot
	bne	a0,a3,2b

/*
 * word copy loop
 */
wwordcopy:
	and	a3,a2,~(NBPW-1)		# word chunks
	subu	a2,a3			# count after by word loop
	beq	a3,zero,wbytecopy	# less than a word to copy
	addu	a3,a0			# source endpoint
1:	lw	v0,0(a0)
	addu	a0,NBPW
	sw	v0,0(a1)
	addu	a1,NBPW*2		# dst += 4; BD slot
	bne	a0,a3,1b
	b	wbytecopy

/*
 * deal with simultaneously unalignable copy by aligning dst
 */
wunaligncopy:
	subu	a3,zero,a1		# calc byte cnt to get dst aligned
	and	a3,NBPW-1		# alignment = 0..3
	subu	a2,a3			# bcount -= alignment
	beq	a3,zero,wpartaligncopy	# already aligned
#ifdef MIPSEB
	lwl	v0,0(a0)		# get whole word
	lwr	v0,3(a0)		# for sure
	swl	v0,0(a1)		# store left piece (1-3 bytes)
#endif
#ifdef MIPSEL
	lwr	v0,0(a0)		# get whole word
	lwl	v0,3(a0)		# for sure
	swr	v0,0(a1)		# store right piece (1-3 bytes)
#endif
	addu	a0,a3			# src += alignment (will fill LD slot)
	addu	a1,a3			# dst += alignment
	add	a1,4
	and	a1,0xfffffff8	        # JN trmendus hack

/*
 * src unaligned, dst aligned loop
 * NOTE: if MINCOPY >= 7, will always do 1 loop iteration or more
 * if we get here at all
 */
wpartaligncopy:
	and	a3,a2,~(NBPW-1)		# space in word chunks
	subu	a2,a3			# count after by word loop
#if MINCOPY < 7
	beq	a3,zero,wbytecopy	# less than a word to copy
#endif
	addu	a3,a0			# source endpoint
1:
#ifdef MIPSEB
	lwl	v0,0(a0)
	lwr	v0,3(a0)
#endif
#ifdef MIPSEL
	lwr	v0,0(a0)
	lwl	v0,3(a0)
#endif
	addu	a0,NBPW
	sw	v0,0(a1)
	addu	a1,NBPW*2
	bne	a0,a3,1b


/*
 * brute force byte copy loop, for bcount < MINCOPY + tail of unaligned dst
 * note that lwl, lwr, swr CANNOT be used for tail, since the lwr might
 * cross page boundary and give spurious address exception
 */
wbytecopy:
	addu	a3,a2,a0		# source endpoint; BDSLOT
	ble	a2,zero,wcopydone	# nothing left to copy, or bad length
1:	lb	v0,0(a0)
	addu	a0,1
	sb	v0,0(a1)
	addu	a1,1			# BDSLOT: incr dst address
	bne	a0,a3,1b
wcopydone:
	j	ra
	END(kn230_wbcopy)
